<!DOCTYPE html>
<html lang="en">

<!-- Mirrored from digitonaut.com/art/procedural_liquid_metal/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 10:58:39 GMT -->
<!-- Added by HTTrack --><meta http-equiv="content-type" content="text/html;charset=UTF-8" /><!-- /Added by HTTrack -->
<head>
<meta charset='utf-8'\>
<meta name='apple-mobile-web-app-capable' content='yes'><meta name='apple-mobile-web-app-status-bar-style' content='black-translucent'><title>Procedural Liquid Metal</title>

<script src="shader-web-background.min.js"></script>

<script type="x-shader/x-fragment" id="Image">
precision highp float;
uniform vec2  iResolution;
uniform float iTime;

// -------------------------------------------------------------------------------------------------- Paste your Shadertoy code here:

//by @paulofalcao

float inObj(vec3 p){
  float oP=length(p);
  p.x=sin(p.x)+sin(iTime);
  p.z=sin(p.z)+cos(iTime);
  return min(length(p)-1.5-sin(oP-iTime*4.0),p.y+3.0);
}

void cameraSetup(out vec3 prp, out vec3 scp, in vec2 fragCoord){
  vec2 iRes=iResolution.xy;
  vec2 vPos=-1.0+2.0*fragCoord.xy/iRes;

  float zf=cos(iTime*0.2)*0.4+0.6;
  vec3 vuv=vec3(sin(iTime),1,0);
  vec3 vrp=vec3(sin(iTime*0.7)*10.0,0,cos(iTime*0.9)*10.0)*zf; 
  prp=vec3(
      (sin(iTime*0.7)*20.0+20.0)*zf,
       sin(iTime)*4.0+7.0,
      (cos(iTime*0.6)*20.0+14.0)*zf)+vrp;

  vec3 vpn=normalize(vrp-prp);
  vec3 u=normalize(cross(vuv,vpn));
  vec3 v=cross(vpn,u);
  vec3 scrCoord=vpn+vPos.x*u*iRes.x/iRes.y+vPos.y*v;
  scp=normalize(scrCoord);
}

void raymarching( out vec4 fragColor, in vec3 prp, in vec3 scp){
  const vec3 e = vec3(0.1,0,0);
  const float maxd=80.0;

  float s=0.1;
  vec3 c,p,n;

  float f=-(prp.y-2.5)/scp.y;
  if (f>0.0) p=prp+scp*f;
  else f=maxd;

  vec3 outc=vec3(0,0,0);

  float far=0.0;
  for (int ref=0;ref<=1;ref++){
    if (ref>0){
      scp=reflect(scp,n);
      prp=p;
      s=0.1;f=0.1;
    }

    for(int i=0;i<32;i++){
      f+=s;
      p=prp+scp*f;
      s=inObj(p);
      if (abs(s)<.01||f>maxd||p.y>2.5||(ref>0&&i>16)) break;
    }
   
    if (f<maxd&&p.y<2.5){
      if(p.y<-2.5){
        if (fract(p.x/4.0)>.5)
          if (fract(p.z/4.0)>.5)
            c=vec3(0,0,0);
          else
            c=vec3(1,1,1);
        else
          if (fract(p.z/4.0)>.5)
            c=vec3(1,1,1);
          else
            c=vec3(0,0,0);
        c=c*max(inObj(vec3(p.x,p.y+1.0,p.z)),0.5);
        n=vec3(0,1,0);
      }
      else{
        float d=length(p);
        c=vec3((sin(d*.25-iTime*4.0)+1.0)/2.0,
               (sin(iTime)+1.0)/2.0,
               (sin(d-iTime*4.0)+1.0)/2.0);
        n=normalize(
          vec3(s-inObj(p-e.xyy),
               s-inObj(p-e.yxy),
               s-inObj(p-e.yyx)));
      }
      float b=dot(n,normalize(prp-p));
      if (ref==0) {
        outc=((b+0.2)*c+pow(b,54.0))*0.7;
        far=1.0-f*.01;
    } else {
      if (prp.y>-2.4) outc+=(b+0.2)*c*0.3;
    }
    }
    else break;
  }
  fragColor=vec4(outc*far,1.0);
}

void mainVR( out vec4 fragColor, in vec2 fragCoord, in vec3 fragRayOri, in vec3 fragRayDir ){
  vec3 prp=(fragRayOri+vec3(0,1,0))*5.0;
  vec3 scp=fragRayDir;
  raymarching(fragColor,prp,scp);
}

void mainImage( out vec4 fragColor, in vec2 fragCoord ){
  vec3 prp;
  vec3 scp;
  cameraSetup(prp,scp,fragCoord);
  raymarching(fragColor,prp,scp);
}

// -------------------------------------------------------------------------------------------------- End of Shadertoy code


void main() {
mainImage(gl_FragColor, gl_FragCoord.xy);
}
</script>
<script>
shaderWebBackground.shade({
	shaders: {
		Image: {
			uniforms: {
				iResolution: (gl, loc, ctx) => gl.uniform2f(loc, ctx.width, ctx.height),
				iTime: (gl, loc) => gl.uniform1f(loc, performance.now() / 1000)
			}
		}
	}
});
</script>
</head>
<body>
</body>

<!-- Mirrored from digitonaut.com/art/procedural_liquid_metal/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 10:58:40 GMT -->
</html>
