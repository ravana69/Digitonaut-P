<!DOCTYPE html>
<html lang="en">

<!-- Mirrored from digitonaut.com/art/paisley/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 10:48:17 GMT -->
<!-- Added by HTTrack --><meta http-equiv="content-type" content="text/html;charset=UTF-8" /><!-- /Added by HTTrack -->
<head>
<meta charset='utf-8'\>
<meta name='apple-mobile-web-app-capable' content='yes'><meta name='apple-mobile-web-app-status-bar-style' content='black-translucent'><title>Paisley</title>

<link type="text/css" rel="stylesheet" href="main.css">
</head>
<body>

<script id="vs" type="not-js">
void main()	{
  gl_Position = vec4( position, 1.0 );
}
</script>
<script id="fs" type="not-js">
precision mediump float;

// -------------------------------------------- code from https://glslsandbox.com/ ------------------
/*
 * Original shader from: https://www.shadertoy.com/view/wldcRn
 */

#ifdef GL_ES
precision mediump float;
#endif

// glslsandbox uniforms
uniform float time;
uniform vec2 resolution;

// shadertoy emulation
#define iTime time
#define iResolution resolution

// --------[ Original ShaderToy begins here ]---------- //
#define Rot(a) mat2(cos(a),-sin(a),sin(a),cos(a))
#define antialiasing(n) n/min(iResolution.y,iResolution.x)
#define S(d,b) smoothstep(antialiasing(1.0),b,d)
#define BASE_COLOR vec3(0.3,0.6,0.3)

float sdBox( in vec2 p, in vec2 b )
{
    vec2 d = abs(p)-b;
    return length(max(d,0.0)) + min(max(d.x,d.y),0.0);
}

// http://mercury.sexy/hg_sdf/
vec2 pModPolar(inout vec2 p, float repetitions, float fix) {
    float angle = 2.0*3.14159265/repetitions;
    float a = atan(p.y, p.x) + angle/2.;
    float r = length(p);
    float c = floor(a/angle);
    a = mod(a,angle) - angle/2.*fix;
    p = vec2(-cos(a), sin(a))*r;

    return p;
}

vec2 bend(vec2 p, float k){
    float c = cos(k*p.y);
    float s = sin(k*p.y);
    mat2  m = mat2(c,-s,s,c);
    vec2  q = p*m;
    return q;
}

float sdUnevenCapsule( vec2 p, float r1, float r2, float h )
{
    p.x = abs(p.x);
    float b = (r1-r2)/h;
    float a = sqrt(1.0-b*b);
    float k = dot(p,vec2(-b,a));
    if( k < 0.0 ) return length(p) - r1;
    if( k > a*h ) return length(p-vec2(0.0,h)) - r2;
    return dot(p, vec2(a,b) ) - r1;
}

float paiselyDist(vec2 p, float sy, float scale) {
    vec2  q = bend(p,1.5);

    q.y*=sy*scale;
    q*=0.8*scale;
    float d = sdUnevenCapsule(q,0.15,0.02*scale,0.35*scale);
    return d;
}

vec3 paiselyTex(vec2 p, vec3 col, float dir) {
    vec3 baseCol = BASE_COLOR;
    vec2 prevP = p;
    float t = mod(iTime,8000.0);
    p*= 0.5;
    
    p*=Rot(radians(t*30.0*dir));
    p.yx = pModPolar(p,14.0,1.0);
    float d = sdBox(p,vec2(0.00001,0.05))-0.007;
    
    p = prevP;
    float d2 = length(p)-0.06;
    d = max(-d2,d);
    
    col = mix(col,baseCol,S(d,0.0));
    
    d = length(p)-0.03;
    col = mix(col,baseCol,S(d,0.0));
    return col;
}

vec3 paisely(vec2 p, vec3 col) {
    vec3 baseCol = BASE_COLOR;
    vec2 pos = vec2(0.0,-0.1);
    vec2 prevP = p;
    float d = abs(paiselyDist(p-pos,0.9,0.88))-0.005;
    float d2 = abs(paiselyDist(p-pos,0.87,1.05))-0.001;
    float d3 = abs(paiselyDist(p-pos,0.85,1.25))-0.005;
    col = mix(col,baseCol,S(d,0.0));
    col = mix(col,baseCol*1.2,S(d2,0.0));
    col = mix(col,baseCol,S(d3,0.0));
    
    p*=3.2;
    col = paiselyTex(p-vec2(0.4,0.55),col,1.0);
    
    p = prevP;
    p*=1.8;
    col = paiselyTex(p-vec2(0.11,0.15),col,-1.0);
    
    p = prevP;
    col = paiselyTex(p-vec2(0.01,-0.11),col,-1.0);
    
    p = prevP;
    p*=3.5;
    col = paiselyTex(p-vec2(-0.13,0.13),col,1.0);
    
    p = prevP;
    p*=2.8;
    col = paiselyTex(p-vec2(0.1,-0.82),col,-1.0);
    
    p = prevP;
    p*=3.2;
    col = paiselyTex(p-vec2(-0.2,-0.89),col,1.0);
    
    p = prevP;
    p*=3.2;
    col = paiselyTex(p-vec2(0.4,-0.78),col,1.0);
        
    p = prevP;
    p*=4.2;
    col = paiselyTex(p-vec2(1.15,1.25),col,1.0);
    
    p = prevP;
    
    p.x -=0.01;
    p.x = abs(p.x);
    p.x -= 0.15;
    d = length(p-vec2(0.01,0.0))-0.02;
    col = mix(col,baseCol,S(d,0.0));
    
    p = prevP;
    p.x = abs(p.x);
    p.x -= 0.18;
    d = length(p-vec2(0.0,-0.12))-0.02;
    col = mix(col,baseCol,S(d,0.0));
    
    p = prevP;
    d = length(p-vec2(-0.14,-0.22))-0.02;
    col = mix(col,baseCol,S(d,0.0));
    
    d = length(p-vec2(-0.072,0.11))-0.02;
    col = mix(col,baseCol,S(d,0.0));
    
    d = length(p-vec2(0.063,0.215))-0.013;
    col = mix(col,baseCol,S(d,0.0));
        
    d = length(p-vec2(0.187,0.13))-0.017;
    col = mix(col,baseCol,S(d,0.0));
    
    return col;
}

vec3 renderTexture(vec2 p, vec3 col) {
    p*=1.3;
    
    vec2 prevP = p;
    
    p.x = mod(p.x,1.0)-0.5;
    p.y = mod(p.y,0.8)-0.4;
    col = paisely(p,col);
    p = prevP;
        
    p.y+=0.1;
    p.x+=0.475;
    p.x = mod(p.x,1.0)-0.5;
    p.y = mod(p.y,0.8)-0.4;
    
    p*=1.2;
    col = paisely(p*Rot(radians(-180.0)),col);
    p = prevP;
    
    p.x+=0.3;
    p.y+=-0.35;
    p.x = mod(p.x,1.0)-0.5;
    p.y = mod(p.y,0.8)-0.4;
    p*=1.8;
    col = paisely(p*Rot(radians(110.0)),col);
    p = prevP;
    
    p.x+=0.06;
    p.y+=-0.32;
    p.x = mod(p.x,1.0)-0.5;
    p.y = mod(p.y,0.8)-0.4;
    p*=1.2;
    col = paiselyTex(p,col,1.0);
    
    return  col;
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
    vec2 uv = (fragCoord-.5*iResolution.xy)/iResolution.y;
    float t = iTime*0.1;
    
    uv.y+=t;
    
    vec2 prevUV = uv;
    vec3 col = vec3(0.99,0.98,0.95);

    col = renderTexture(uv,col);
    
    fragColor = vec4(col,1.0);
}
// --------[ Original ShaderToy ends here ]---------- //

void main(void)
{
    mainImage(gl_FragColor, gl_FragCoord.xy);
}

// -------------------------------------------------------------------------------------------------------------------

</script>
<script src="three.min.js"></script>
<script src="main.js"></script>

</body>

<!-- Mirrored from digitonaut.com/art/paisley/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 10:48:22 GMT -->
</html>
