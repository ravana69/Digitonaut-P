<!DOCTYPE html>

<!-- Mirrored from digitonaut.com/art/potentially/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 10:58:30 GMT -->
<!-- Added by HTTrack --><meta http-equiv="content-type" content="text/html;charset=UTF-8" /><!-- /Added by HTTrack -->
<head>
<meta charset='utf-8'\>
<meta name='apple-mobile-web-app-capable' content='yes'><meta name='apple-mobile-web-app-status-bar-style' content='black-translucent'><title>Potentially</title>

<script src="shader-web-background.min.js"></script>
<script type="x-shader/x-fragment" id="Image">
precision highp float;
uniform vec2  iResolution;
uniform float iTime;

// -------------------------------------------------------------------------------------------------- Shadertoy code
// CC-BY-NC-SA
#define N_CHARGES 137.0
#define SEED -31415.
#define SPEED 0.75

float N21(vec2 p, float offset) {  // shamelessly taken from https://www.shadertoy.com/view/lscczl, CC-BY-NC-SA
	vec3 a = fract(vec3(p.xyx) * (vec3(213.897, 653.453, 253.098) + offset));
    a += dot(a, a.yzx + 79.76);
    return fract((a.x + a.y) * a.z);
}

vec2 N22(vec2 p) {  // forgot the definition in the video, but I guess this one's okay
    return vec2(N21(p, 32.), N21(p, -314.159));
}

vec2 N12(float seed) {
    return N22(vec2(seed));
}

float N11(float seed) {  // yes, yes, I know, horribly inefficient, sue me
    return N21(vec2(seed, 1./seed), seed);
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
    vec2 uv_ar = fragCoord/iResolution.y;  // aspect ratio 1:1, but x > 1 possible
    float aspect = iResolution.x/iResolution.y;
    
    float potential = 0.;    
    for(float n_charge=0.; n_charge < N_CHARGES; n_charge++) {
        float x_omega_charge = N11(n_charge + SEED + N_CHARGES + 1./137.);
        float y_omega_charge = N11(n_charge - SEED + N_CHARGES - 1./137.);
        float t = SPEED * iTime + SEED;
        vec2 uv_charge = 0.5*vec2(sin(x_omega_charge*t), cos(y_omega_charge*t))+.5;
        float charge = (N11(n_charge + SEED - N_CHARGES)-.5)*1e2;
        float r = max(.0005 * abs(charge), length(uv_ar - uv_charge*vec2(aspect, 1.)));
        potential += charge/r;
    }

    float l2 = log(1. + abs(potential)/100.)/4.;
    float l1 = -sign(potential) * l2;
    float l3 = sign(potential) * l2;
    vec3 col = vec3(l1, length(uv_ar-.5)/2., l3);
    
    fragColor = vec4(col,1.0);
}
// -------------------------------------------------------------------------------------------------- End of Shadertoy code

void main() {
	mainImage(gl_FragColor, gl_FragCoord.xy);
}
</script>
<script>
shaderWebBackground.shade({
	shaders: {
		Image: {
			uniforms: {
				iResolution: (gl, loc, ctx) => gl.uniform2f(loc, ctx.width, ctx.height),
				iTime: (gl, loc) => gl.uniform1f(loc, performance.now() / 1000)
			}
		}
	}
});
</script>
</head>
<body>
</body>

<!-- Mirrored from digitonaut.com/art/potentially/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 10:58:31 GMT -->
</html>
