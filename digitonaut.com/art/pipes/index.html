<!DOCTYPE html>
<html lang="en">

<!-- Mirrored from digitonaut.com/art/pipes/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 10:50:20 GMT -->
<!-- Added by HTTrack --><meta http-equiv="content-type" content="text/html;charset=UTF-8" /><!-- /Added by HTTrack -->
<head>
<meta charset='utf-8'\>
<meta name='apple-mobile-web-app-capable' content='yes'><meta name='apple-mobile-web-app-status-bar-style' content='black-translucent'><title>Pipes</title>

<script src="shader-web-background.min.js"></script>

<script type="x-shader/x-fragment" id="Image">
precision highp float;
uniform vec2  iResolution;
uniform float iTime;

// -------------------------------------------------------------------------------------------------- Paste your Shadertoy code here:
// Created by XORXOR, 2016
// Attribution-NonCommercial-ShareAlike 4.0 International (CC BY-NC-SA 4.0)
//
// Shadertoy remix of davidope's work
// http://dvdp.tumblr.com/post/97344550933/140913

#define ANTIALIAS

float cylinder( vec3 p )
{
    return length( p.xy ) - 0.9;
}

vec3 opTwist( vec3 p )
{
    float a = -0.1 * iTime - 0.02 * p.z;
    float c = cos( a );
    float s = sin( a );
    mat2 m = mat2( c, -s, s, c );
    return vec3( m * p.xy, p.z );
}

vec2 opRep( vec2 p, float size, float start, float stop )
{
    float halfSize = size * 0.5;
    vec2 c = floor( p / size );
    p = mod( p, size ) - halfSize;
    if ( ( c.x > stop ) || ( c.y > stop ) )
    {
        p += size * ( c - stop );
    }
    if ( ( c.x < start ) || ( c.y < start ) )
    {
        p += size * ( c - start );
    }
    return p;
}

float map( vec3 p )
{
    p = opTwist( p );
    p.xy = opRep( p.xy, 10.0, -7.0, 7.0 );
    float d = cylinder( p );
    return d;
}

float scene( vec3 ro, vec3 rd )
{
    float t = 0.01;
    for ( int i = 0; i < 200; i++ )
    {
        vec3 p = ro + t * rd;
        float d = map( p );
        if ( d < 0.001 )
        {
            return t;
        }
        t += d;
    }
    return -1.0;
}

vec3 calcNormal( vec3 pos )
{
    vec3 eps = vec3( 0.001, 0.0, 0.0 );
    float pd = map( pos );
    vec3 n = vec3(
            pd - map( pos - eps.xyy ),
            pd - map( pos - eps.yxy ),
            pd - map( pos - eps.yyx ) );
    return normalize( n );
}

float calcAo( vec3 pos, vec3 n )
{
    float occ = 0.0;
    for ( int i = 0; i < 5; i++ )
    {
        float hp = 0.1 + 2.0 * float( i );
        float dp = map( pos + n * hp );
        occ += ( hp - dp );
    }
    return clamp( 1.0 - 0.04 * occ, 0.0, 1.0 );
}

vec3 render( vec3 ro, vec3 rd )
{
    float d = scene( ro, rd );
    vec3 col = vec3( 0 );
    if ( d > 0.0 )
    {
        vec3 pos = ro + d * rd;
        vec3 twistPos = opTwist( pos );
        float t = -15.0 * iTime;
        col = vec3( floor( 0.3 * mod( twistPos.z + t, 6.0 ) ) );
        vec3 nor = calcNormal( pos );
        float ao = calcAo( pos, nor );
        col *= vec3( ao );
        float fog = ( 400.0 + pos.z ) / 300.0;
        col *= fog;
    }
	return col;
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
    vec3 eye = vec3( 0.0, 0.0, 20.0 );
    vec3 target = vec3( 0.0 );
    vec3 cw = normalize( target - eye );
    vec3 cu = cross( cw, vec3( 0, 1, 0 ) );
    vec3 cv = cross( cu, cw );
    mat3 cm = mat3( cu, cv, cw );

    vec3 col = vec3( 0.0 );

        vec2 off = vec2( 0.0 );

vec2 uv = ( fragCoord.xy + off - 0.5 * iResolution.xy ) / iResolution.y;
        vec3 rd = cm * normalize( vec3( uv, 2.5 ) );

        col += render( eye, rd );
    col *= 0.75;

    fragColor = vec4( col, 1.0 );
}


// -------------------------------------------------------------------------------------------------- End of Shadertoy code


void main() {
mainImage(gl_FragColor, gl_FragCoord.xy);
}
</script>
<script>
shaderWebBackground.shade({
	shaders: {
		Image: {
			uniforms: {
				iResolution: (gl, loc, ctx) => gl.uniform2f(loc, ctx.width, ctx.height),
				iTime: (gl, loc) => gl.uniform1f(loc, performance.now() / 1000)
			}
		}
	}
});
</script>
</head>
<body>
</body>

<!-- Mirrored from digitonaut.com/art/pipes/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 10:50:20 GMT -->
</html>
